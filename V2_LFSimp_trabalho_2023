lingSimples - trabalho 2023 - versão 2

Nesta versão da linguagem, várias formações de expressões são admitidas no parser, mas não podem ser computadas (avaliadas) porque infringem os tipos. O 'if' agora tem uma condição que é um valor booleano ou o uma expressão relacional.

Software disponível (acessado em out/2023) para escrever/reconhecer a sintaxe de linguagens
 
 https://bnfplayground.pauliankline.com/

/* gramática utilizada no trabalho 2 - 2023 */
/* a gramática abaixo pode ser submetida ao software sugerido para que vocês possam entender quais expressões são aceitas na linguagem */

<character>     ::= <letter> | <digit> | <symbol>
<letter>        ::= [a-z] | [A-Z] 
<digit>         ::= [0-9]
<intnum>        ::= <digit>+
<number>        ::= <intnum> | <intnum> "." <intnum>
<bool>          ::= "true" | "false"
<other_symb>    ::=  "_" | "!" | "?" | "#" 
<reserv_symb>   ::=   "(" | ")" | "+" | "-" | "*" | "~" | "%" |
                      "<" | ">" | "<=" | ">=" | "==" | "!=" 
<symbol>        ::=  <reserv_symb> | <other_symb>
<reserv_word>   ::=  "cons" | "head" | "tail" | 
                     "if" | "let" | "letrec" | 
                     "lambda" | "call" |
                     <bool>
                      
<reservedW>      ::=  <reserv_symb> | <reserv_word>

/* id a ser modificado - começa com letra, seguida de letras e dígitos e (não é uma palavra reservada) */
/* já realizado no primeiro trabalho */

<id>             ::=  (<character>)+

/* -- Operadores Relacionais -- */
<eq_op>  ::= " == " | " != "
<cmp_op> ::= " > " | " >= " | " < " | " <= "  
<op_rel> ::= <eq_op> | <cmp_op>

/* -- Expressões Relacionais -- */
<rel_expr> ::= "(" <op_rel>  <arith_expr> " " <arith_expr> ")" 


/* -- Operadores Aritméticos -- */
<op_arith_bin> ::= " + " | " - " | " * " | " % " 
<op_arith_un>  ::= " ~ "

/* -- Expressões Aritméticas -- */
<arith_expr> ::= "(" <exprA_bin> ")" | "(" <exprA_un> ")" | <number>
<exprA_bin>  ::= <op_arith_bin> <code> " " <code>
<exprA_un>   ::= <op_arith_un> <code> 


/* -- Operadores Listas -- */
<op_list>      ::= "head " | "tail "

/* -- Expressões sobre listas -- */
<list_expr>  ::= <cons> | "(" <exprL>  ")"
<cons>       ::= "(cons " <code> " " <code> ")"
<exprL>      ::= <op_list>  <code> 


/* -- Expressões Lambda (definição e aplicação de funções) -- */
<lamb_expr> ::=  <lambda> | <call> 
<lambda>    ::= "(lambda " <param> " "  <code> ")"
<param>     ::= <id>
<call>      ::= "(call " <lambda> " " <code> ")"

/* -- expressões if -- */
<if>    ::= "(if " <cond>  " " <pos>  " " <neg> ")"
<cond>  ::= <rel_expr> | <bool>
<pos>   ::= <code>
<neg>   ::= <code>

/* -- expressões let  -- */         
<let_expr>  ::=  <let> | <letrec> 
<let>       ::= "(let " <id> " "  <def>  " " <body> ")"
<letrec>    ::= "(letrec " <id> " "  <lambda>  " " <body> ")"
<def>       ::= <code>
<body>      ::= <code>


/* -- Código do programa -- */
<code>  ::=  <expr> | <number>

<expr>  ::=  <arith_expr> | <list_expr> | <lamb_expr> | <if> | <let_expr> 

/* -- expressões quote -- */
<quote>     ::=  <code> | <id>

/* -- PALAVRAS RECONHECIDAS PELO ID SEM RESERVADAS -- */

<caracter>		    	::= [A-Z] | [a-z]
<digito>            	::= [0-9]
<alfanum>           	::= <caracter> | <digito>

<caracter_sem_a>		::= [B-Z] | [b-z] | <digito>
<caracter_sem_b>		::= "A" | [C-Z] | "a" | [c-z] | <digito>
<caracter_sem_c>		::= [A-Z] | [a-b] | [d-z] | <digito>
<caracter_sem_d>		::= [A-Z] | [a-c] | [e-z] | <digito>
<caracter_sem_e>		::= [A-Z] | [a-d] | [f-z] | <digito>
<caracter_sem_f>		::= [A-Z] | [a-e] | [g-z] | <digito>
<caracter_sem_h>		::= [A-Z] | [a-g] | [i-z] | <digito> 
<caracter_sem_i>		::= [A-Z] | [a-h] | [j-z] | <digito>
<caracter_sem_l>		::= [A-Z] | [a-k] | [m-z] | <digito>
<caracter_sem_m>		::= [A-Z] | [a-l] | [n-z] | <digito>
<caracter_sem_n>		::= [A-Z] | [a-m] | [o-z] | <digito>
<caracter_sem_o>		::= [A-Z] | [a-n] | [p-z] | <digito>
<caracter_sem_r>		::= [A-Z] | [a-q] | [s-z] | <digito>
<caracter_sem_s>		::= [A-Z] | [a-r] | [t-z] | <digito>
<caracter_sem_t>		::= [A-Z] | [a-s] | [u-z] | <digito>
<caracter_sem_u>		::= [A-Z] | [a-t] | [v-z] | <digito>

<caracter_sem_iniciais> ::= [A-Z] | [a-b] | [d-e] | "g" | [j-k] | [m-s] | [u-z]

<caracter_sem_a_o>		::= [A-Z] | [b-n] | [p-z]
<caracter_sem_a_e>		::= [A-Z] | [b-d] | [f-z]
<caracter_sem_a_r>		::= [A-Z] | [b-q] | [s-z]
            
<string_clean>          ::= <caracter> | <caracter_sem_iniciais> <alfanum>* | "ca" <caracter_sem_l> <alfanum>* | "cal" <caracter_sem_l> <alfanum>* | 
                        "call" <alfanum>+ | "ca" | "cal" | "c" <caracter_sem_a_o> <alfanum>* | "co" <caracter_sem_n> <alfanum>* | "con" <caracter_sem_s> <alfanum>* | 
                        "cons" <alfanum>+ | "co" | "con" | "l" <caracter_sem_a_e> <alfanum>* | "la" <caracter_sem_m> <alfanum>* | "lam" <caracter_sem_b> <alfanum>* |
                        "lamb" <caracter_sem_d> <alfanum>* | "lambd" <caracter_sem_a> <alfanum>* | "lambda" <alfanum>+ | "la" | "lam" | "lamb" | "lambd" |
                        "le" <caracter_sem_t> <alfanum>* | "le" | "let" <caracter_sem_r> <alfanum>* | "letr" <caracter_sem_e> <alfanum>* |
                        "letre" <caracter_sem_c> <alfanum>* | "letrec" <alfanum>+ | "letr" | "letre" | "t" | "ta" | "tai" | "t" <caracter_sem_a_r> <alfanum>* |
                        "ta" <caracter_sem_i> <alfanum>* | "tai" <caracter_sem_l> <alfanum>* | "tail" <alfanum>+ | "tr" <caracter_sem_u> <alfanum>* |
                        "tru" <caracter_sem_e> <alfanum>* | "true" <alfanum>+ | "tr" | "tru" | "h" <caracter_sem_e> <alfanum>* | "he" <caracter_sem_a> <alfanum>* |
                        "hea" <caracter_sem_d> <alfanum>* | "head" <alfanum>+ | "he" | "hea" | "i" <caracter_sem_f> <alfanum>* | "if" <alfanum>+ |
                        "f" <caracter_sem_a> <alfanum>* | "fa" <caracter_sem_l> <alfanum>* | "fal" <caracter_sem_s> <alfanum>* | "fals" <caracter_sem_e> <alfanum>* |  
                        "false" <alfanum>+ | "fa" | "fal" | "fals" 